#include "../imports/all_imports.fc";

#include "storage.fc";
#include "get_methods.fc";


() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    slice sender_address = cs~load_msg_addr();

    load_data();

    if (flags & 1) { ;; bounced messages
        ;; TODO
        return ();
    }

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    int pool_id = in_msg_body~load_uint(32);
    force_pool(FACTORY_ADDRESS, sender_address, pool_id);

    if (~ init?) {  ;; op == op::SET_DATA
        int ref_exists = in_msg_body~load_int(1);
        if (ref_exists) {
            slice referrer_address = in_msg_body~load_ref().begin_parse();
            cell referrer_wallet_code = in_msg_body~load_maybe_ref();
            cell referrer_wallet_state_init = calculate_referrer_wallet_state_init(referrer_address, referrer_wallet_code);
            storage::referrer_wallet_address = get_address_by_state_init(referrer_wallet_state_init);
        }
        else {
            storage::referrer_wallet_address = null_addr();
        }
        save_data();
        return ();
    }

    raw_reserve(tons::MIN_RESERVE, 2);
    if ((op == op::REQUEST_UPDATE_REFERRER) & (~ equal_slices(storage::referrer_wallet_address, null_addr()))) {
        int balance_change = in_msg_body~load_int(121);

        (slice balance_slice, int found) = storage::balances.udict_get?(32, pool_id);
        int balance = found ? balance_slice~load_coins() : 0;
        balance += balance_change;
        if (balance < 0) {
            balance_change = balance - balance_change;
            balance = 0;
        }
        
        if (balance_change != 0) {
            cell payload = begin_cell()
                            .store_uint(op, 32)
                            .store_uint(query_id, 64)
                            .store_slice(storage::owner_address)
                            .store_uint(pool_id, 32)
                            .store_int(balance_change, 121)
                        .end_cell();
            send_cell_message(storage::referrer_wallet_address, 0, payload, mode::CARRY_REMAINING_BALANCE);
            
            storage::balances~udict_set_builder(32, pool_id, begin_cell().store_coins(balance));
            save_data();
            return ();
        }
    }

    send_excess(storage::owner_address, query_id, 0, mode::CARRY_REMAINING_BALANCE);
    return ();
}
